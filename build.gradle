import com.axelor.gradle.support.ChangelogSupport

plugins {
	id 'com.axelor.app'
}

apply from: 'version.gradle'
apply plugin: ChangelogSupport

try {
    tasks.named('spotlessCheck')
} catch (UnknownTaskException ignored) {
    apply from: './gradle/style.gradle'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

group = "com.axelor.apps"

axelor {
	title "Axelor Portal"
	description "Axelor Portal Module"
}

changelog {
  version = baseVersion
  output.set(file('CHANGELOG.md'))
  inputPath.set(file('changelogs/unreleased'))
  types.set(['Feature', 'Change', 'Deprecate', 'Remove', 'Fix', 'Security'])
  header.set("${version.get()} (${new Date().format('yyyy-MM-dd')})")
}

dependencies {
    if (findProject(':modules:axelor-project') != null) {
        api project(':modules:axelor-project')
    } else {
        api "com.axelor.apps:axelor-project:${aosVersion}"
    }

    if (findProject(':modules:axelor-production') != null) {
        api project(':modules:axelor-production')
    } else {
        api "com.axelor.apps:axelor-production:${aosVersion}"
    }

    if (findProject(':modules:axelor-helpdesk') != null) {
        api project(':modules:axelor-helpdesk')
    } else {
        api "com.axelor.apps:axelor-helpdesk:${aosVersion}"
    }

    if (findProject(":modules:axelor-human-resource") != null) {
        api project(":modules:axelor-human-resource")
    }else {
        api "com.axelor.apps:axelor-human-resource:${aosVersion}"
    }


    if (findProject(':modules:axelor-mattermost') != null) {
        api project(':modules:axelor-mattermost')
    } else {
        api ('com.axelor.apps:axelor-mattermost') {
            version {
                require '[8.0, 8.1['
                prefer '8.0.0'
            }
        }
    }
}

apply plugin: 'maven-publish'

ext {
    mavenUsername = project.findProperty( 'axelorMavenUsername' )
    mavenPassword = project.findProperty( 'axelorMavenPassword' )
}

publishing {
    repositories {
        maven {
			def repoPath = project.hasProperty("finalRelease")
					? "maven-releases"
					: "maven-snapshots";
            name = "maven"
            url = "https://repository.axelor.com/nexus/repository/" + repoPath
            credentials {
                username = project.mavenUsername
                password = project.mavenPassword
            }
        }
    }
}
