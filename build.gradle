plugins {
	id 'com.axelor.app'
}

apply from: "version.gradle"

try {
    tasks.named('spotlessCheck')
} catch (UnknownTaskException ignored) {
    apply from: './gradle/style.gradle'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

group = "com.axelor.apps"

axelor {
	title "Axelor Portal"
	description "Axelor Portal Module"
}

ext.aosVersion = '8.4.0'

dependencies {
    if (findProject(':modules:axelor-production') != null) {
        api project(':modules:axelor-production')
    } else {
        implementation "com.axelor.apps:axelor-production:${aosVersion}"
    }

    if (findProject(':modules:axelor-business-support') != null) {
        api project(':modules:axelor-business-support')
    } else {
        implementation "com.axelor.apps:axelor-business-support:${aosVersion}"
    }

    if (findProject(':modules:axelor-helpdesk') != null) {
        api project(':modules:axelor-helpdesk')
    } else {
        implementation "com.axelor.apps:axelor-helpdesk:${aosVersion}"
    }

    if (findProject(':modules:axelor-mattermost') != null) {
        api project(':modules:axelor-mattermost')
    } else {
        implementation "com.axelor.apps:axelor-mattermost:8.0.+"
    }
}

apply plugin: 'maven-publish'

ext {
    mavenUsername = project.findProperty( 'axelorMavenUsername' )
    mavenPassword = project.findProperty( 'axelorMavenPassword' )
}

publishing {
    repositories {
        maven {
			def repoPath = project.hasProperty("finalRelease")
					? "maven-releases"
					: "maven-snapshots";
            name = "maven"
            url = "https://repository.axelor.com/nexus/repository/" + repoPath
            credentials {
                username = project.mavenUsername
                password = project.mavenPassword
            }
        }
    }
}
